not:

output: out
input: in

output=nand input input



and:

output: out
inputA: in
inputB: in

a=nand inputA inputB
output=not a



or:

output: out
inputA: in
inputB: in

a=not inputA
b=not inputB
output=nand a b



xor:

output: out
inputA: in
inputB: in

a=or inputA inputB
b=nand inputA inputB
output=and a b



andABC:

output: out

inputA: in
inputB: in
inputC: in

a=and inputA inputB
output=and a inputC



xorABC:

output: out

inputA: in
inputB: in
inputC: in

a=xor inputA inputB
output=xor a inputC



add:

output: out
outputCarry: out

inputA: in
inputB: in
inputC: in

output=xorABC inputA inputB inputC

a=not inputA
b=andABC a inputB inputC
c=or inputB inputC
d=and inputA c
outputCarry=or b d



add2:

output: out 2
outputCarry: out

inputA: in 2
inputB: in 2
inputCarry: in

a carry=add inputA[0] inputB[0] inputCarry
b outputCarry=add inputA[1] inputB[1] carry
output=concat1_1 a b



add4:

output: out 4
outputCarry: out

inputA: in 4
inputB: in 4
inputCarry: in

a carry=add2 inputA[0:2] inputB[0:2] inputCarry
b outputCarry=add2 inputA[2:4] inputB[2:4] carry
output=concat2_2 a b



add8:

output: out 8
outputCarry: out

inputA: in 8
inputB: in 8
inputCarry: in

a carry=add4 inputA[0:4] inputB[0:4] inputCarry
b outputCarry=add4 inputA[4:8] inputB[4:8] carry
output=concat4_4 a b



add16:

output: out 16
outputCarry: out

inputA: in 16
inputB: in 16
inputCarry: in

a carry=add8 inputA[0:8] inputB[0:8] inputCarry
b outputCarry=add8 inputA[8:16] inputB[8:16] carry
output=concat8_8 a b



main:

output: out 16
outputEnable: out

input: in 16

valueReg: reg 16

outputEnable=set 1

valueReg outputCarry=add16 input valueReg 0

output=set16 valueReg
